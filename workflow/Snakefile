configfile: "config/config.yml"

rule all:
    input:
        expand("outputs/{sample}_aligned.fasta", sample=config["samples"]),
        expand("outputs/{sample}_modeltest.out", sample=config["samples"]),
        expand("outputs/{sample}_my_model.txt", sample=config["samples"]),
        expand("outputs/{sample}.raxml.support", sample=config["samples"]),
        expand("outputs/{sample}_tree.pdf", sample=config["samples"])

rule align:
    input:
        "data/{sample}.fasta"
    output:
        "outputs/{sample}_aligned.fasta"
    conda:
        "envs/mafft.yml"
    shell:
        "mafft {input} > {output}"

rule model:
    input:
        "outputs/{sample}_aligned.fasta"
    output:
        model = "outputs/{sample}_modeltest.out"
    conda:
        "envs/modeltest.yml"
    params:
        output_prefix = subpath(output.model, ".out")
    shell:
        "modeltest-ng -i {input} -o {params.output_prefix}"

rule parse_model:
    input:
        full_text = "outputs/{sample}_modeltest.out"
    output:
        short_text = "outputs/{sample}_my_model.txt"
    run:
        from scripts.get_model import parse_modeltest
        my_model = parse_modeltest(input.full_text)
        with open(output.short_text, "w") as my_output:
            my_output.write(f"{my_model}\n")

rule tree_inference:
    input:
        fasta = "outputs/{sample}_aligned.fasta",
        model_text = "outputs/{sample}_my_model.txt"
    output:
        support = "outputs/{sample}.raxml.support"
    conda:
        "envs/raxml.yml"
    params:
        output_prefix = subpath(output.support, ".raxml.support")
    shell:
        "raxml-ng --all --msa {input.fasta} --model $(cat {input.model_text}) --prefix {params.output_prefix} --bs-trees {config[bootstrap]}"

rule figtree:
    input:
        "outputs/{sample}.raxml.support"
    output:
        "outputs/{sample}_tree.pdf"
    conda:
        "envs/figtree.yml"
    shell:
        "figtree -graphic PNG {input} {output}"

